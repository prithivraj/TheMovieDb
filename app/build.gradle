apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion versions.compileSdkVersion
    buildToolsVersion versions.buildToolsVersion

    defaultConfig {
        applicationId "com.robdir.themoviedb"

        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersions

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "THE_MOVIE_DB_API_KEY", THE_MOVIE_DB_API_KEY
        buildConfigField "String", "BASE_URL", "\"https://api.themoviedb.org/3/\""
        buildConfigField "String", "POSTER_URL", "\"http://image.tmdb.org/t/p/w\""
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            buildConfigField "int", "DATABASE_VERSION", "1"
        }

        release {
            buildConfigField "int", "DATABASE_VERSION", "1"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test{
            java.srcDirs += 'src/test/kotlin'
            java.srcDirs += 'src/test/resources'
        }
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true

            all {
                jvmArgs '-noverify'
            }
        }
    }
}

preBuild.dependsOn ':ktlint'
preBuild.dependsOn ':detekt'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation libraries.kotlin

    implementation libraries.supportLibrary
    implementation libraries.designLibrary
    implementation libraries.cardView

    // Dependency Injection
    implementation libraries.dagger
    implementation libraries.daggerAndroidSupport
    kapt libraries.daggerCompiler
    kapt libraries.daggerAndroidProcessor

    // Rx
    implementation libraries.rxJava
    implementation libraries.rxAndroid

    // Architecture Components
    implementation libraries.room
    kapt libraries.roomCompiler
    implementation libraries.lifecycle
    kapt libraries.lifecycleCompiler

    // Retrofit
    implementation libraries.retrofit
    implementation libraries.retrofitGsonConverter
    implementation libraries.retrofitAdapter
    implementation libraries.httpLoggingInterceptor

    // Picasso
    implementation libraries.picasso

    // Testing
    testImplementation libraries.junit
    testImplementation libraries.mockitoCore
    testImplementation libraries.archCoreTesting
    androidTestImplementation libraries.testRunner
    androidTestImplementation libraries.kotlinTest
}
